plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
}

// Feature flag: enable AI runtime packaging. Default = false (build without bundling heavy AI runtimes).
def aiEnabled = project.hasProperty('aiEnabled') ? project.property('aiEnabled').toString().toLowerCase() == 'true' : false

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
	maven { url 'https://maven.wispforest.io/releases/' }
	flatDir {
		dirs "./lib"
	}
}

fabricApi {
	configureDataGeneration()
}

loom {
	runs {
		client {
			ideConfigGenerated true
			runDir "run"
		}
	}
}

dependencies {
	// Core Minecraft/Fabric
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Core application dependencies
	// AI libraries: compile-time only by default so code compiles but runtime/native packaging can be toggled
	compileOnly 'com.github.amithkoujalgi:ollama4j:1.0.77'
	implementation 'com.github.FasterXML:jackson-core:jackson-core-2.17.2'
	implementation 'com.github.FasterXML:jackson-databind:jackson-databind-2.17.2'
	implementation 'com.github.FasterXML:jackson-datatype-jsr310:jackson-datatype-jsr310-2.8.4'
	implementation 'com.github.FasterXML:jackson-annotations:jackson-annotations-2.17.2'
	implementation "org.xerial:sqlite-jdbc:3.50.2.0"
	implementation 'org.apache.commons:commons-math3:3.6.1'
	// https://mvnrepository.com/artifact/org.apache.commons/commons-compress
	implementation 'org.apache.commons:commons-compress:1.28.0'
	implementation 'org.jsoup:jsoup:1.21.1'

	// DJL dependencies (compile-time only; native runtimes are added to packaging when aiEnabled=true)
	compileOnly "ai.djl:api:0.33.0"
	compileOnly "ai.djl.huggingface:tokenizers:0.33.0"
	compileOnly "ai.djl.pytorch:pytorch-engine:0.33.0"
	if (aiEnabled) {
		runtimeOnly "ai.djl.pytorch:pytorch-native-cpu:2.5.1:linux-x86_64"
		runtimeOnly "ai.djl.pytorch:pytorch-native-cpu:2.5.1:win-x86_64"
		runtimeOnly "ai.djl.pytorch:pytorch-native-cpu:2.5.1:osx-aarch64"
	}
	compileOnly "ai.djl.pytorch:pytorch-model-zoo:0.33.0"

	// OpenNLP ONLY - No Stanford CoreNLP!
	implementation 'org.apache.opennlp:opennlp-tools:2.5.5'


	// Include statements for runtime packaging. AI runtime pieces are included only when aiEnabled=true.
	include "com.github.FasterXML:jackson-databind:jackson-databind-2.17.2"
	include "com.github.FasterXML:jackson-core:jackson-core-2.17.2"
	include "com.github.FasterXML:jackson-datatype-jsr310:jackson-datatype-jsr310-2.8.4"
	include "com.github.FasterXML:jackson-annotations:jackson-annotations-2.17.2"
	include "org.xerial:sqlite-jdbc:3.50.2.0"
	include 'org.apache.commons:commons-math3:3.6.1'
	include "org.apache.commons:commons-compress:1.28.0"
	include 'org.jsoup:jsoup:1.21.1'
	if (aiEnabled) {
		include "com.github.amithkoujalgi:ollama4j:1.0.77"
		include "ai.djl:api:0.33.0"
		include "ai.djl.huggingface:tokenizers:0.33.0"
		include "ai.djl.pytorch:pytorch-engine:0.33.0"
		include "ai.djl.pytorch:pytorch-native-cpu:2.5.1:linux-x86_64"
		include "ai.djl.pytorch:pytorch-native-cpu:2.5.1:win-x86_64"
		include "ai.djl.pytorch:pytorch-native-cpu:2.5.1:osx-aarch64"
		include "ai.djl.pytorch:pytorch-model-zoo:0.33.0"
	}
	include 'org.apache.opennlp:opennlp-tools:2.5.5'

	dependencies {
		implementation fileTree(dir: 'libs', include: ['*.jar'])
	}
}

shadowJar {
	dependencies {
		include(dependency('com.fasterxml.jackson.core:jackson-databind'))
		include(dependency("io.wispforest:owo-sentinel:0.12.6+1.20.3"))
		if (aiEnabled) {
			include(dependency("com.github.amithkoujalgi:ollama4j:1.0.77"))
		}
	}
	relocate 'com.fasterxml.jackson', 'net.shasankp000.relocated.com.fasterxml.jackson'
	relocate 'io.wispforest:owo-sentinel', 'net.shasankp000.relocated.io.wispforest:owo-sentinel'
	relocate 'com.github.amithkoujalgi', 'net.shasankp000.relocated:com.github.amithkoujalgi'
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}
	repositories {
		// Add repositories to publish to here.
	}
}

// Minimal JVM configuration - no complex XML parsing needed
tasks.withType(JavaExec).configureEach {
	if (name.contains("run")) {
		// Basic module access for Java 17+
		jvmArgs += "--add-opens=java.base/java.lang=ALL-UNNAMED"
		jvmArgs += "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED"

		// Performance
		jvmArgs += "-Xmx4G"
		jvmArgs += "-Xms2G"
	}
}
